<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="FB_Homing" Id="{e8b732be-06ce-4dd5-944d-e7ad6eae8e67}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Homing
VAR_INPUT
    bExecute: BOOL;
    bReset: BOOL;
    bStop: BOOL;
    nHomeProc: UINT;
    bLimitBwd: BOOL;
    bLimitFwd: BOOL;
    bEncLatch: BOOL;
    bHomeSensor: BOOL;
    fHomePosition: LREAL;
END_VAR
VAR_IN_OUT
    Axis: AXIS_REF;
END_VAR
VAR_OUTPUT
    bHomed:BOOL;
    bError: BOOL;
    bWarning: BOOL;
    nErrorId: UDINT;
END_VAR
VAR
    bHomingInProgress: BOOL;
    bReadNcVelocities: BOOL;
    nHomeProcLocal: UINT := 0;
    nHomingState: INT := 0;
    fbExecuteRiseEdge: R_TRIG;
    fbHomingParameter: MC_HomingParameter;
    fbLimitSwRef: MC_Ref_Signal_Ref;
    fbLimitSwRef2: MC_Ref_Signal_Ref;
    fbHomeSwRef: MC_Ref_Signal_Ref;
    fbIndexRef: MC_Ref_Signal_Ref;
    stOptions2: ST_Home_Options2;
    stOptions4: ST_Home_Options4;
    tLimit: TIME :=  T#0H10M;
    fbStepLimitSwitch: MC_StepLimitSwitch;
    fbStepLimitSwitchDetection: MC_StepLimitSwitchDetection;
    fbStepAbsoluteSwitch: MC_StepAbsoluteSwitch;
    fbStepReferencePulse: MC_StepReferencePulse;
    fbHomeDirect: MC_HomeDirect;
    fbAbortHoming: MC_AbortHoming;
    fbFinishHoming: MC_FinishHoming;
    fbHomeReadNCVelocities: FB_HomeReadNCVelocities;

    stTrigger: TRIGGER_REF;
    fbTouchProbe: MC_TouchProbe;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bReset THEN
    bError:=FALSE;
    bWarning:=FALSE;
    bReadNcVelocities:=FALSE;
    bHomingInProgress:=FALSE;
    fbAbortHoming.Execute:=FALSE;
    nErrorId:=0;
    nHomingState:=0;
END_IF

IF bStop THEN
    bReadNcVelocities:=FALSE;
    bHomingInProgress:=FALSE;
    nHomingState:=0;
END_IF

fbExecuteRiseEdge(CLK:=bExecute);
IF fbExecuteRiseEdge.Q THEN
    bHomingInProgress:=TRUE;
    bReadNcVelocities:=TRUE;
    fbAbortHoming.Execute:=FALSE;
    nHomeProcLocal:=nHomeProc;
    CASE nHomeProcLocal OF
      //0X: LIMIT Switch
      01: //Home to bBwdLimit (reverse off limit)
      02: //Home to bFwdLimit (reverse off limit)
      03: //Home to bBwdLimit (two speeds)
      04: //Home to bFwdLimit (two speeds)
      //1X: Home Sensor
      11: //Home to bHome in Bwd direction (single speed - will reverse back if limit hit)
      12: //Home to bHome in Fwd direction (single speed - will reverse back if limit hit)
      13: //Home to bHome via bBwdLimit (two speeds)
      14: //Home to bHome via bFwdLimit (two speeds)
      //2X: Enc Pulse
      21: //Home to EncRefPulse in Bwd direction (single speed - will reverse back if limit hit)
      22: //Home to EncRefPulse in Fwd direction (single speed - will reverse back if limit hit)
      23: //Home to EncRefPulse via bBwdLimit (two speeds)
      24: //Home to EncRefPulse via bFwdLimit (two speeds)
      //9X: Software
      90: //Home direct to value
    ELSE
      bError:=TRUE;
      bHomingInProgress := FALSE;
      nErrorId:=16#4FFF;
  END_CASE
END_IF

IF bHomingInProgress AND fbHomeReadNcVelocities.bDone AND NOT bError THEN
CASE nHomeProcLocal OF
   1,2: //Home to bLimitBwd(1) or bLimitFwd(2)
        CASE nHomingState OF
          0:
            fbStepLimitSwitch.Execute:=FALSE;
            fbStepLimitSwitch.LimitSwitchMode:=mcFallingEdge;
            fbStepLimitSwitch.Velocity:=fbHomeReadNcVelocities.fVelocityFromCam;
            fbStepLimitSwitch.Setposition:=fHomePosition;
            CASE nHomeProcLocal OF
                1: fbStepLimitSwitch.Direction:=mcNegativeDirection;
                2: fbStepLimitSwitch.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepLimitSwitch.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepLimitSwitch.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepLimitSwitch.Done THEN
                fbFinishHoming.Execute:=TRUE;
                nHomingState:=nHomingState+1;
            END_IF
          4:
            IF fbFinishHoming.Done THEN
                bHomingInProgress:=FALSE;
                fbFinishHoming.Execute:=FALSE;
                bReadNcVelocities:=FALSE;
                nHomingState:=0;
            END_IF
        END_CASE
   3,4: //Home to bLimitBwd(3) or bLimitFwd(4) using two speeds
        CASE nHomingState OF
          0:
            fbStepLimitSwitchDetection.Execute:=FALSE;
            fbStepLimitSwitchDetection.LimitSwitchMode:=mcRisingEdge;
            fbStepLimitSwitchDetection.Velocity:=fbHomeReadNCVelocities.fVelocityToCam;
            CASE nHomeProcLocal OF
                3: fbStepLimitSwitchDetection.Direction:=mcNegativeDirection;
                4: fbStepLimitSwitchDetection.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepLimitSwitchDetection.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepLimitSwitchDetection.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepLimitSwitchDetection.Done THEN
                fbStepLimitSwitch.Execute:=FALSE;
                fbStepLimitSwitch.LimitSwitchMode:=mcFallingEdge;
                fbStepLimitSwitch.Velocity:=fbHomeReadNCVelocities.fVelocityFromCam;
                fbStepLimitSwitch.Setposition:=fHomePosition;
                CASE nHomeProcLocal OF
                    3: fbStepLimitSwitch.Direction:=mcNegativeDirection;
                    4: fbStepLimitSwitch.Direction:=mcPositiveDirection;
                END_CASE
                nHomingState:=nHomingState+1;
            END_IF
          4:
            fbStepLimitSwitch.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          5:
            IF fbStepLimitSwitch.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          6:
            IF fbStepLimitSwitch.Done THEN
                fbFinishHoming.Execute:=TRUE;
                nHomingState:=nHomingState+1;
            END_IF
          7:
            IF fbFinishHoming.Done THEN
                bHomingInProgress:=FALSE;
                fbFinishHoming.Execute:=FALSE;
                bReadNCVelocities:=FALSE;
                nHomingState:=0;
            END_IF
        END_CASE
    11,12: //Home to bHome Sensor moving in the negative(11) or positive(12) direction
        CASE nHomingState OF
          0:
            fbStepAbsoluteSwitch.Execute:=FALSE;
            fbStepAbsoluteSwitch.SwitchMode:=mcRisingEdge;
            fbStepAbsoluteSwitch.SetPosition:=fHomePosition;
            fbStepAbsoluteSwitch.Velocity:=fbHomeReadNCVelocities.fVelocityFromCam;
            CASE nHomeProcLocal OF
                11: fbStepAbsoluteSwitch.Direction:=mcNegativeDirection;
                12: fbStepAbsoluteSwitch.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepAbsoluteSwitch.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepAbsoluteSwitch.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepAbsoluteSwitch.Done THEN
                fbStepAbsoluteSwitch.Execute:=FALSE;
                fbFinishHoming.Execute:=TRUE;
                nHomingState:=nHomingState+1;
            END_IF
          4:
            IF fbFinishHoming.Done THEN
                bHomingInProgress:=FALSE;
                fbFinishHoming.Execute:=FALSE;
                bReadNCVelocities:=FALSE;
                nHomingState:=0;
            END_IF
        END_CASE
    13,14: //Home to bHomeSensor via bLimitBwd(13) or bLimitFwd(14)
        CASE nHomingState OF
          0:
            fbStepLimitSwitchDetection.Execute:=FALSE;
            fbStepLimitSwitchDetection.LimitSwitchMode:=mcRisingEdge;
            fbStepLimitSwitchDetection.Velocity:=fbHomeReadNCVelocities.fVelocityToCam;
            CASE nHomeProcLocal OF
                13: fbStepLimitSwitchDetection.Direction:=mcNegativeDirection;
                14: fbStepLimitSwitchDetection.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepLimitSwitchDetection.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepLimitSwitchDetection.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepLimitSwitchDetection.Done THEN
                fbStepLimitSwitchDetection.Execute:=FALSE;
                CASE nHomeProcLocal OF
                    13: nHomeProcLocal:=12;
                    14: nHomeProcLocal:=11;
                END_CASE
                nHomingState:=0;
            END_IF
        END_CASE
    21,22: //Home to index pulse of an encoder in the negative(21) or positive(22) direction
        CASE nHomingState OF
          0:
            fbStepReferencePulse.Execute:=FALSE;
            fbStepReferencePulse.SetPosition:=fHomePosition;
            fbStepReferencePulse.Velocity:=fbHomeReadNCVelocities.fVelocityFromCam;
            CASE nHomeProcLocal OF
                21: fbStepReferencePulse.Direction:=mcNegativeDirection;
                22: fbStepReferencePulse.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepReferencePulse.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepReferencePulse.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepReferencePulse.Done THEN
                fbStepReferencePulse.Execute:=FALSE;
                fbFinishHoming.Execute:=TRUE;
                nHomingState:=nHomingState+1;
            END_IF
          4:
            IF fbFinishHoming.Done THEN
                bHomingInProgress:=FALSE;
                fbFinishHoming.Execute:=FALSE;
                bReadNCVelocities:=FALSE;
                nHomingState:=0;
            END_IF
        END_CASE
    23,24: //Home to index pulse of an encoder via bLimitBwd(23) or bLimitFwd(24)
        CASE nHomingState OF
          0:
            fbStepLimitSwitchDetection.Execute:=FALSE;
            fbStepLimitSwitchDetection.LimitSwitchMode:=mcRisingEdge;
            fbStepLimitSwitchDetection.Velocity:=fbHomeReadNCVelocities.fVelocityToCam;
            CASE nHomeProcLocal OF
                23: fbStepLimitSwitchDetection.Direction:=mcNegativeDirection;
                24: fbStepLimitSwitchDetection.Direction:=mcPositiveDirection;
            END_CASE
            nHomingState:=nHomingState+1;
          1:
            fbStepLimitSwitchDetection.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2:
            IF fbStepLimitSwitchDetection.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3:
            IF fbStepLimitSwitchDetection.Done THEN
                fbStepLimitSwitchDetection.Execute:=FALSE;
                CASE nHomeProcLocal OF
                    23: nHomeProcLocal:=22;
                    24: nHomeProcLocal:=21;
                END_CASE
                nHomingState:=0;
            END_IF
        END_CASE
    90: //Set current position without moving
       CASE nHomingState OF
          0:
            fbHomeDirect.Execute:=FALSE;
            fbHomeDirect.SetPosition:=fHomePosition;
            nHomingState:=nHomingState+1;
          1:
            fbHomeDirect.Execute:=TRUE;
            nHomingState:=nHomingState+1;
          2: // Check that started
            IF fbHomeDirect.Busy THEN
                nHomingState:=nHomingState+1;
            END_IF
          3: // Check that finished
            IF fbHomeDirect.Done THEN
                fbHomeDirect.Execute:=FALSE;
                bHomingInProgress:=FALSE;
                bReadNCVelocities:=FALSE;
                nHomingState:=0;
            END_IF
        END_CASE
END_CASE
ELSE
    fbStepLimitSwitch.Execute:=FALSE;
    fbStepLimitSwitchDetection.Execute:=FALSE;
    fbStepAbsoluteSwitch.Execute:=FALSE;
    fbStepReferencePulse.Execute:=FALSE;
    fbHomeDirect.Execute:=FALSE;
    nHomingState:=0;
END_IF

fbHomeReadNCVelocities(
    En:=TRUE,
    bExecute:=bReadNCVelocities,
    bReset:=bReset,
    Axis:=Axis );

StOptions2.DisableDriveAccess:=TRUE;
StOptions4.DisableDriveAccess:=TRUE;
stOptions4.EnableLagErrorDetection:=TRUE;

IF fbStepLimitSwitch.Direction=mcPositiveDirection THEN
    fbLimitSwRef.Level:=NOT(bLimitFwd);
ELSIF fbStepLimitSwitch.Direction=mcNegativeDirection THEN
    fbLimitSwRef.Level:=NOT(bLimitBwd);
END_IF
fbStepLimitSwitch(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    LimitSwitchSignal:= fbLimitSwRef,
    Options:= StOptions4);

IF fbStepLimitSwitchDetection.Direction=mcPositiveDirection THEN
    fbLimitSwRef2.Level:=NOT(bLimitFwd);
ELSIF fbStepLimitSwitchDetection.Direction=mcNegativeDirection THEN
    fbLimitSwRef2.Level:=NOT(bLimitBwd);
END_IF
fbStepLimitSwitchDetection(
    Axis:= AXIS,
    Parameter:= fbHomingParameter,
    Execute:= ,
    Direction:= ,
    LimitSwitchMode:= ,
    LimitSwitchSignal:= fbLimitSwRef2,
    TimeLimit:= tLimit,
    Options:= StOptions4);

fbHomeSwRef.Level:=bHomeSensor;
fbHomeSwRef.TouchProbe:=PlcEvent;
fbHomeSwRef.SignalSource:=SignalSource_Default;
fbStepAbsoluteSwitch(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    ReferenceSignal:= fbHomeSwRef,
    TimeLimit:= tLimit,
    PositiveLimitSwitch:=NOT(bLimitFwd),
    NegativeLimitSwitch:=NOT(bLimitBwd),
    Options:= StOptions4);

fbIndexRef.SignalSource:=SignalSource_ZeroPulse;
fbIndexRef.TouchProbe:=TouchProbe1;
fbStepReferencePulse(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    ReferenceSignal:= fbIndexRef,
    TimeLimit:= tLimit,
    Options:= StOptions4);

fbHomeDirect(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    Options:= StOptions2);

fbFinishHoming(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    Velocity:=,
    Options:= StOptions2);

fbAbortHoming(
    Axis:= Axis,
    Parameter:= fbHomingParameter,
    Options:= StOptions2);

IF fbStepLimitSwitch.Error THEN
    bError:=TRUE;
    nErrorId:=fbStepLimitSwitch.ErrorID;
ELSIF fbStepLimitSwitchDetection.Error THEN
    bError:=TRUE;
    nErrorId:=fbStepLimitSwitchDetection.ErrorID;
ELSIF fbStepAbsoluteSwitch.Error THEN
    bError:=TRUE;
    nErrorId:=fbStepAbsoluteSwitch.ErrorID;
ELSIF fbStepReferencePulse.Error THEN
    bError:=TRUE;
    nErrorId:=fbStepReferencePulse.ErrorID;
ELSIF fbHomeDirect.Error THEN
    bError:=TRUE;
    nErrorId:=fbHomeDirect.ErrorID;
ELSIF fbAbortHoming.Error THEN
    bError:=TRUE;
    nErrorId:=fbAbortHoming.ErrorID;
ELSIF fbFinishHoming.Error THEN
    bError:=TRUE;
    nErrorId:=fbFinishHoming.ErrorID;
END_IF

IF fbStepLimitSwitch.CommandAborted OR fbStepLimitSwitchDetection.CommandAborted OR fbStepAbsoluteSwitch.CommandAborted OR fbStepReferencePulse.CommandAborted OR fbHomeDirect.CommandAborted THEN
    nHomingState:=0;
    bHomingInProgress:=FALSE;
    bWarning:=TRUE;
END_IF

bHomed:=Axis.Status.Homed;]]></ST>
    </Implementation>
    <LineIds Name="FB_Homing">
      <LineId Id="3852" Count="10" />
      <LineId Id="3864" Count="373" />
      <LineId Id="905" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>